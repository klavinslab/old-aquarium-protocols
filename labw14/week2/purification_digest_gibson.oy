#each team gets their own primers and plasmids

argument
  primer1: sample, "The first primer"
  primer2: sample, "The second primer"
  plasmid_id: sample, "The plasmid stock"
  enzyme_id: sample, "The Phsion HF Master Mix stock"
#  ladder: sample, "The ladder for your gel"
end
ladder = 822


place pcr_prep
  protocol: "labw14/week2/pcr_prepare.pl"
  argument
    primer1: primer1
    primer2: primer2
    plasmid_id: plasmid_id
    enzyme_id: enzyme_id
  end
  group: aquarium_user
  marked: true
  start: now()
  window: minutes(30)
end

#stagger
place pour_gel
  protocol: "labw14/week2/pour_gel.pl"
  argument
    percentage: 1
    comb_n: 1
  end
  group: aquarium_user
  start: now()
  window: minutes(120)
end

#first step is wait until thermal cycler is done
place pcr_finish
  protocol: "labw14/week2/pcr_finish.pl"
  group: aquarium_user
  start: minutes(70)
  window: minutes(1)
end

#first step is make sure gel has solidified 
place run_gel
  protocol: "labw14/week2/run_gel.pl"
  argument
    ladder_one: ladder
    fragment_volume: 50
  end
  group: aquarium_user
  start: now()
  window: minutes(1)
end

wire (pcr_finish,"PCR_Result_id") => (run_gel,"fragment_one")

place image_cut_gel
  protocol: "labw14/week2/image_cut_gel.pl"
  group: aquarium_user
  start: minutes(40)
  window: minutes(1)
end

wire (run_gel,"Gel_lane_id") => (image_cut_gel,"gel")

place clean_gel_box
  protocol: "labw14/week2/clean_gel_box.pl"
  group: aquarium_user
  start: now()
  window: minutes(1)
end


# forward transitions
transition [pcr_prep]     => [pour_gel,pcr_finish] when !error(0) && completed(0) end
transition [pour_gel, pcr_finish] => [run_gel] when !error(0) && completed(0) && !error(1) && completed(1) end
transition [run_gel] => [image_cut_gel] when !error(0) && completed(0) end
transition [image_cut_gel] => [clean_gel_box] when !error(0) && completed(0) end
transition [clean_gel_box] => [] when !error(0) && completed(0) end



# rework transitions
transition [pcr_prep]     => [pcr_prep]    when error(0) end
transition [pour_gel]     => [pour_gel]     when error(0) end
transition [pcr_finish]     => [pcr_finish]     when error(0) end
transition [run_gel]     => [run_gel]     when error(0) end
transition [image_cut_gel] => [image_cut_gel]    when error(0) end
transition [clean_gel_box] => [clean_gel_box]    when error(0) end

