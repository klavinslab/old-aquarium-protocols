# 
# glycerol_stock_creation.oy
#
# Created: 2/3/2014
# Authors: Patrick Ma, Pramod Chavali
#
# Metacol covers the procedures required to take plated colonies and create a glycerol
# suspension stock for storage.

argument
  Transformed_E_coli_Strain_id: sample, "The sample you wish to use"
end

# select colonies from plates, isolate and multiply
place suspension
  protocol: "labw14/group4/colony_transfer_pcpm.pl"
  group: aquarium_user
  start: now()
  marked: true
  argument
    Transformed_E_coli_Strain_id: Transformed_E_coli_Strain_id
  end
end

# Taking the colonies previously isolated, transfer and create the actual stock aliquots
#
# somewhat arbitrary window; according to paper used as procedure source, there is not a 
# defined upper limit to incubation. However, too long and too many cells will grow.
place glycerol_stock
  protocol: "labw14/group4/glycerol_stock.pl"
  group: aquarium_user
  start: hours(18)
  window: hours(4) 
end

wire (suspension, "suspended_cells_id") => (glycerol_stock, "culture_tube")

# proceed forward
transition [suspension] => [glycerol_stock] when !error(0) && completed(0) end

# Error occured in first procedure
transition [suspension] => [suspension] when error(0) end

# error occured in second procedure
transition [glycerol_stock] => [glycerol_stock] when error(0) end

# Metacol complete
transition [glycerol_stock] => [] when !error(0) && completed(0) end



